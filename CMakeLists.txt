cmake_minimum_required(VERSION 3.18)

project(qgama LANGUAGES CXX VERSION "2.02")

set(QGAMA_CONFIGURE_NAME      ${PROJECT_NAME})
set(QGAMA_CONFIGURE_VERSION   ${PROJECT_VERSION})
set(QGAMA_CONFIGURE_COPYRIGHT "2022")

configure_file ("${CMAKE_CURRENT_SOURCE_DIR}/configure.h.in"
                "${CMAKE_CURRENT_SOURCE_DIR}/qgama/configure.h")

if (NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/gama)
    message("Qgama cannot be build without GNU Gama project")
    message("You can get the source codes from GIT or FTP server")
    message("   * git clone git://git.savannah.gnu.org/gama.git")
    message("   * ftp://ftp.gnu.org/gnu/gama")

    message(FATAL_ERROR
            "Directory ${CMAKE_CURRENT_SOURCE_DIR}/gama is missing")
endif()

set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)


# CMAKE_PREFIX_PATH  By default this is empty. It is intended to be set by
# the project. If you use Qt Creator to build the project, you can use
# CMakeLists.txt as it is.
#
# Linux
#
#set(CMAKE_PREFIX_PATH "~/Qt/6.2.0/gcc_64/")
#
# or set up the environment variable CMAKE_PREFIX_PATH, for example
#
#   $ export CMAKE_PREFIX_PATH=~/Qt/6.2.0/gcc_64/
#   $ cmake .. -G Ninja
#   $ cmake --build .
#
# Windows
#
#set(CMAKE_PREFIX_PATH "C:/Qt/6.2.2/msvc2019_64/")
#
# On Windows you can build qgama either with QtCreator, Visual Studio
# or simply using cmake (On Windows cmake uses Visual Studio as the
# implicit generator). Anyway you would have to set the
# CMAKE_PREFIX_PATH properly. Also you would probably need to specify
# if you want Release build or Debug build (implicit).
#
# From the PowerShell prompt run
#
#   > cmake ..
#   > cmake --build . --config Release
#
# The resulting executable is found in the folder ./Release/qgama.exe.
#
# Run windeployqt.exe to copy all dll libraries needed by your executable
#
# C:\Qt\6.2.2\msvc2019_64\bin\windeployqt.exe .\Release\qgama.exe


# QtCreator supports the following variables for Android, which are
# identical to qmake Android variables.
# Check http://doc.qt.io/qt-5/deployment-android.html for more information.
# They need to be set before the find_package(Qt5 ...) call.

#if(ANDROID)
#    set(ANDROID_PACKAGE_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/android")
#    if (ANDROID_ABI STREQUAL "armeabi-v7a")
#        set(ANDROID_EXTRA_LIBS
#            ${CMAKE_CURRENT_SOURCE_DIR}/path/to/libcrypto.so
#            ${CMAKE_CURRENT_SOURCE_DIR}/path/to/libssl.so)
#    endif()
#endif()


add_definitions(-DGNU_gama_expat_1_1)  # if we use local copy of expat sources
include_directories(gama/lib qgama)

find_package(Qt6 COMPONENTS
  Core Gui Sql Widgets PrintSupport SvgWidgets REQUIRED)

set(KRUMM   # support for Krumm input network files
    gama/lib/krumm/common.cpp
    gama/lib/krumm/common.h
    gama/lib/krumm/input.cpp
    gama/lib/krumm/input.h
    gama/lib/krumm/k2gkf.cpp
    gama/lib/krumm/k2gkf.h
    gama/lib/krumm/output.cpp
    gama/lib/krumm/output.h
    gama/lib/krumm/string_matrix.cpp
    gama/lib/krumm/string_matrix.h
)

set(QGSRC
    qgama/main.cpp
    qgama/adjustment.cpp
    qgama/adjustment.h
    qgama/adjustmentresults.cpp
    qgama/adjustmentresults.h
    qgama/constants.cpp
    qgama/constants.h
    qgama/dbconnection.cpp
    qgama/dbconnection.h
    qgama/dbfunctions.cpp
    qgama/dbfunctions.h
    qgama/drawsettings.cpp
    qgama/drawsettings.h
    qgama/importconfigurationfile.cpp
    qgama/importconfigurationfile.h
    qgama/importkrummnetworkfile.cpp
    qgama/importkrummnetworkfile.h
    qgama/insertclusterdialog.cpp
    qgama/insertclusterdialog.h
    qgama/insertobservationdialog.cpp
    qgama/insertobservationdialog.h
    qgama/lineeditdelegate.cpp
    qgama/lineeditdelegate.h
    qgama/networkadjustmentpanel.cpp
    qgama/networkadjustmentpanel.h
    qgama/networksvg.cpp
    qgama/networksvg.h
    qgama/observationeditor.cpp
    qgama/observationeditor.h
    qgama/observationtablemodel.cpp
    qgama/observationtablemodel.h
    qgama/parametereditor.cpp
    qgama/parametereditor.h
    qgama/pointeditor.cpp
    qgama/pointeditor.h
    qgama/pointtablemodel.cpp
    qgama/pointtablemodel.h
    qgama/pointtypecombobox.cpp
    qgama/pointtypecombobox.h
    qgama/qgamacontrolpanel.cpp
    qgama/qgamacontrolpanel.h
    qgama/qgamahelp.cpp
    qgama/qgamahelp.h
    qgama/qgamainterfaces.h
    qgama/selectadjresultslanguage.cpp
    qgama/selectadjresultslanguage.h
    qgama/selectconfiguration.cpp
    qgama/selectconfiguration.h
    qgama/showmessage.h
    qgama/shrinkbandwidth.cpp
    qgama/shrinkbandwidth.h
    qgama/tableviewstyle.cpp
    qgama/tableviewstyle.h
    qgama/xml2txt.cpp
    qgama/xml2txt.h
    qgama/qgama.qrc
    ${KRUMM}
    )

if(ANDROID)
  add_library(qgama SHARED
    ${QGSRC}
    $<TARGET_OBJECTS:libgama>
  )
else()
  add_executable(qgama
    ${QGSRC}
    $<TARGET_OBJECTS:libgama>
  )
endif()

target_link_libraries(qgama PRIVATE
  Qt6::Widgets Qt6::Sql Qt6::PrintSupport Qt6::SvgWidgets)

# Building with plugins by default. If you don't want to build plugins,
# use the following option to disable them

OPTION(BUILD_PLUGINS "Build with plugins." OFF)
if(BUILD_PLUGINS)
    add_subdirectory(qgama-plugins/adjustmentdemoplugin)
    add_subdirectory(qgama-plugins/dbdemoplugin)
    target_link_libraries(adjustmentdemoplugin)
    target_link_libraries(dbdemoplugin)
endif(BUILD_PLUGINS)

set (GAMA_INSTDIR bin)
add_subdirectory(gama)  # git clone https://git.savannah.gnu.org/git/gama.git

install(TARGETS qgama DESTINATION .)
install(FILES README.md DESTINATION .)

include(CPack)
